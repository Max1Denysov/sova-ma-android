apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
// Apply the Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'
// Apply the Performance Monitoring plugin
apply plugin: 'com.google.firebase.firebase-perf'

Properties localProps = new Properties()
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    localProps.load(localPropsFile.newDataInputStream())
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.osmino.sova"
        minSdkVersion 23
        targetSdkVersion 29

        def versionMajor = localProps.getProperty("major") ?: System.getenv("MAJOR") ?: 1
        def versionMinor = localProps.getProperty("minor") ?: System.getenv("MINOR") ?: 0
        def ciBuildNumber = localProps.getProperty("buildNumber") ?: System.getenv("CI_PIPELINE_IID") ?: 18
        versionCode = Integer.valueOf(versionMajor) * 100000 +
                Integer.valueOf(versionMinor) * 1000 +
                Integer.valueOf(ciBuildNumber)
        versionName = "${versionMajor}.${versionMinor}.${ciBuildNumber}"

        archivesBaseName ="sova-$versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        ci {
            storeFile file("sova.jks")
            storePassword localProps.getProperty("storePass") ?: System.getenv("STORE_PASSWORD")
            keyAlias "sova"
            keyPassword localProps.getProperty("keyPass") ?: System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.ci
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.ci
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

dependencies {
    def versions_room = "2.2.5"
    def versions_retrofit = "2.9.0"
    def versions_okhttp = '4.8.1'
    def work_version = "2.4.0"
    def leakcanary_version = "2.5"

    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'androidx.navigation:navigation-fragment:2.3.1'
    implementation 'androidx.navigation:navigation-ui:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.paging:paging-runtime:2.1.2'

    implementation "androidx.room:room-runtime:$versions_room"
    annotationProcessor "androidx.room:room-compiler:$versions_room"
    implementation "androidx.room:room-rxjava2:$versions_room"

    implementation "androidx.work:work-runtime:$work_version"

    implementation "com.squareup.okhttp3:okhttp:$versions_okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$versions_okhttp"
    implementation "com.squareup.retrofit2:retrofit:$versions_retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions_retrofit"

    implementation 'com.google.firebase:firebase-analytics:17.6.0'
    implementation 'com.google.firebase:firebase-perf:19.0.9'
    implementation 'com.google.firebase:firebase-crashlytics:17.2.2'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-object-watcher-android:$leakcanary_version"

    implementation project(':chatkit')

    implementation 'com.facebook.fresco:fresco:2.2.0'

    implementation 'com.github.daniel-stoneuk:material-about-library:3.1.2'

    implementation "com.github.skydoves:preferenceroom:1.2.0"
    annotationProcessor "com.github.skydoves:preferenceroom-processor:1.2.0"

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation "androidx.room:room-testing:$versions_room"
}
